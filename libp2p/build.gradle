apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'cn.vbyte.p2p'
version = '1.2.6'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 8
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'libp2p interface for android'
        websiteUrl = "https://github.com/Vbytes/libp2p-interface"
        issueTrackerUrl = "https://github.com/Vbytes/libp2p-interface/issues"
        vcsUrl = "https://github.com/Vbytes/libp2p-interface.git"
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'p2p']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name = project.name
            packaging = 'aar'
            url = 'https://github.com/Vbytes/libp2p-interface'
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'vbytes'
                    name 'Vbytes tech'
                    email 'repository@exatech.cn'
                }
            }
            scm {
                connection 'https://github.com/Vbytes/libp2p-interface.git'
                developerConnection 'https://github.com/Vbytes/libp2p-interface.git'
                url 'https://github.com/Vbytes/libp2p-interface'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
